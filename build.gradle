/*
 * Copyright 2014 - 2016 Real Logic Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

defaultTasks 'clean', 'build', 'shadowJar', 'install'

def agronaGroup = 'org.agrona'
def agronaVersion = '0.9.3-SNAPSHOT'

group = agronaGroup
version = agronaVersion
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

ext {
    group = agronaGroup
    version = agronaVersion

    if (!project.hasProperty('ossrhUsername')) {
        ossrhUsername = ''
    }

    if (!project.hasProperty('ossrhPassword')) {
        ossrhPassword = ''
    }
}

def projectPom = {
    name = 'agrona'
    packaging = 'pom'
    // optionally artifactId can be defined here
    description = 'High performance primitives and utility library.'
    url = 'https://github.com/real-logic/Agrona'

    scm {
        connection = 'scm:git:https://github.com/real-logic/Agrona.git'
        developerConnection = 'scm:git:https://github.com/real-logic/Agrona.git'
        url = 'https://github.com/real-logic/Agrona.git'
    }

    licenses {
        license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id = 'tmontgomery'
            name = 'Todd L. Montgomery'
            email = 'tmont@nard.net'
            url = 'https://github.com/tmontgomery'
        }
        developer {
            id = 'mjpt777'
            name = 'Martin Thompson'
            email = 'mjpt777@gmail.com'
            url = 'https://github.com/mjpt777'
        }
        developer {
            id = 'RichardWarburton'
            name = 'Richard Warburton'
            email = 'richard.warburton@gmail.com'
            url = 'https://github.com/RichardWarburton'
        }
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'checkstyle'
    apply plugin: 'signing'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'eclipse'

    group = agronaGroup
    version = agronaVersion

    repositories {
        mavenCentral()
        maven {
            url "http://sevntu-checkstyle.github.com/sevntu.checkstyle/maven2"
        }
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle:7.5.1'
        checkstyle 'com.github.sevntu.checkstyle:sevntu-checks:1.23.0'

        testCompile(
                'org.hamcrest:hamcrest-all:1.3',
                'junit:junit:4.12',
                'org.mockito:mockito-core:2.6.8',
        )
    }

    checkstyle {
        configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
        toolVersion = '7.5.1'
    }

    javadoc {
        title = '<h1>Agrona</h1>'
        options.bottom = '<i>Copyright &#169; 2014 - 2017 Real Logic Ltd. All Rights Reserved.</i>'
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
        sign configurations.shadow
    }
}

project(':agrona') {

    apply plugin: 'osgi'

    compileJava {
        // Suppress warnings about using Unsafe and sun.misc
        options.compilerArgs << '-XDignore.symbol.file'
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }


    def generatedDir = file("${buildDir}/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }
    // Code Generation Configuration
    compileGeneratedJava.dependsOn 'generatePrimitiveExpansions'
    compileGeneratedJava.classpath += sourceSets.main.runtimeClasspath


    task(generatePrimitiveExpansions, type: JavaExec) {
        main = 'org.agrona.generation.PrimitiveExpander'
        classpath = sourceSets.main.runtimeClasspath
        outputs.dir generatedDir
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
        from sourceSets.generated.allSource
    }

    jar {
        from sourceSets.generated.output
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(':agrona-agent') {

    compileTestJava {
        // Suppress warnings about using Unsafe and sun.misc
        options.compilerArgs << '-XDignore.symbol.file'
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    dependencies {
        compile project(':agrona')
        compile "net.bytebuddy:byte-buddy:1.6.5"
    }

    jar {
        manifest {
            attributes(
                'Premain-Class': 'org.agrona.agent.BufferAlignmentAgent',
                'Agent-Class': 'org.agrona.agent.BufferAlignmentAgent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true'
            )
        }
    }

    shadowJar {
        dependencies {
            exclude(project(':agrona'))
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    uploadShadow {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }

        mustRunAfter 'uploadArchives'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

task uploadToMavenCentral {
    dependsOn 'agrona:uploadArchives', 'agrona-agent:uploadShadow'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
    distributionType = 'ALL'
}
